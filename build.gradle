buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id 'io.franzbecker.gradle-lombok' version '1.11'
    id 'java'
}

apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle.forge'

configurations {
    embed
    compile.extendsFrom(embed)
}

repositories {
    maven { url "https://jitpack.io" }
}

dependencies {
    // embed 'com.flowpowered:flow-math:1.0.3'
    compile 'com.github.DimensionalDevelopment:DimDoors:1c78b5e629'
    compileOnly 'com.github.DimensionalDevelopment:AnnotatedNBT:-SNAPSHOT'
}

// Mod version
version = ext.modversion = "1.0.0"
group = "org.dimdev.dimindustry"
archivesBaseName = "DimensionalIndustry"

// Travis build number
String fullVersion = version
if (System.getenv("TRAVIS_BUILD_NUMBER") != null) {
    fullVersion += "+${System.getenv("TRAVIS_BUILD_NUMBER")}"
} else {
    fullVersion += "+UNOFFICIAL"
}
String jarVersion = fullVersion.replace("+", "-") // Github/Travis doesn't seem to support + in filenames

// Jar name format
archivesBaseName = "DimensionalIndustry"

// Minecraft, MCP, Forge, and Java versions
sourceCompatibility = targetCompatibility = '1.8'
ext.mcversion = "1.12.2"
ext.forgeversion = "14.23.2.2654"
String mcpversion = "snapshot_20180404"

minecraft {
    version = "$mcversion-$forgeversion"
    runDir = "run"
    mappings = mcpversion
    replace '${version}', version
    makeObfSourceJar = false
}

// Tasks
compileJava {
    //options.compilerArgs += "-proc:only" // To debug AnnotatedNBT code generation
}

jar {
    archiveName = archivesBaseName + "-" + jarVersion + ".jar"
    from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveName = archivesBaseName + "-" + jarVersion + "-sources.jar"
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

processResources {
    // This will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // Replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // Copy everything else except mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
