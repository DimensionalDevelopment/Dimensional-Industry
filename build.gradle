buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id 'io.franzbecker.gradle-lombok' version '1.11'
    id 'java'
}

apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle.forge'

repositories {
    maven { url "https://jitpack.io" }
}

dependencies {
    // embed 'com.flowpowered:flow-math:1.0.3'
    compile 'com.github.DimensionalDevelopment:DimDoors:1c78b5e629'
    compileOnly 'com.github.DimensionalDevelopment:AnnotatedNBT:-SNAPSHOT'
}

// Mod version
version = ext.modversion = "1.0.0"
group = "org.dimdev.dimindustry"

// Travis build number
String fullVersion = version
if (System.getenv("TRAVIS_BUILD_NUMBER") != null) {
    fullVersion += "+${System.getenv("TRAVIS_BUILD_NUMBER")}"
} else {
    fullVersion += "+UNOFFICIAL"
}

// Jar name format
archivesBaseName = "DimensionalIndustry"
jar.archiveName = "DimensionalIndustry-" + fullVersion + ".jar"

// Minecraft, MCP, Forge, and Java versions
sourceCompatibility = targetCompatibility = '1.8'
ext.mcversion = "1.12.2"
ext.forgeversion = "14.23.2.2654"
String mcpversion = "snapshot_20180404"

configurations {
    embed
    compile.extendsFrom(embed)
}

// Tasks
compileJava {
    //options.compilerArgs += "-proc:only" // To debug AnnotatedNBT code generation
}

jar {
    from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
}

minecraft {
    version = "$mcversion-$forgeversion"
    runDir = "run"
    mappings = mcpversion
    replace '${version}', version
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
